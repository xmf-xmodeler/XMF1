parserImport XOCL;

import XML;
import Parser;

context Parser

  @Class Or extends Pattern
  
    @Doc
      Succeeds when either the left or the right pattern succeeds.
      Binding and return value occurs due to the successful pattern.
    end
    
    @Attribute left : Pattern end
    @Attribute right : Pattern end
    
    @Constructor(left,right) ! end
    
    @Operation desugar():Performable
      [| XML::Parser::Or(<left.desugar()>,<right.desugar()>) |]
    end
    
    @Operation parse(grammar,env,elements,succ,fail,indent,refTable)
      left.parse(grammar,env,elements,succ,
        @Operation()
          right.parse(grammar,env,elements,succ,fail,indent,refTable)
        end,
        indent,
        refTable)
    end
    
  end