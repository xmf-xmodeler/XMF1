parserImport XOCL;
parserImport XMap;

import Clients;
import Diagrams;
import DiagramsMapping;
import Menus;
import MappingDiagrams;
import MappingMapping;
import XMap;

context MappingMapping

  @Class DomainXDomainEdge extends ElementXEdge, RightClickableProxy
  
    @Attribute map       : Map end
    @Attribute target    : Element end
    @Attribute edge      : DomainEdge (?) end
    
    @Constructor(map,target,edge,owner)
      self.setHotLoad(true);
      self.checkDaemons();
      self.setGlobalMenu(edge,Set{Element,edge.of()})
    end

    @Operation checkDaemons()
      self.checkEdgeDaemons()
    end

    @Operation checkEdgeDaemons()
      @SlotValueChanged multi once DomainXDomainEdge::domainSourceChangedDaemon(edge,"source") end;
      @SlotValueChanged multi once DomainXDomainEdge::domainTargetChangedDaemon(edge,"target") end
    end

    @Operation domainSourceChangedDaemon(edge,slot,newPort,oldPort)
      owner.domainSourceChanged(self,newPort.owner)
    end
    
    @Operation domainTargetChangedDaemon(edge,slot,newPort,oldPort)
      owner.domainTargetChanged(self,newPort.owner)
    end

    @Operation hotLoaded()
      self.checkDaemons();
      self.setGlobalMenu(edge,Set{Element,edge.of()},false)
    end

    @Operation removeDaemons()
      edge.removeDaemonsWithTarget(self,true)
    end

    @Operation element()
      Seq{map,target}
    end

  end  