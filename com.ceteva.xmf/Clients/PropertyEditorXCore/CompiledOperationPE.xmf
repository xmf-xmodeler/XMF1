parserImport XOCL;

import Clients;
import Forms;
import PropertyEditors;
import PropertyEditorXCore;

context PropertyEditorXCore

  @Class CompiledOperationPE extends AbstractObjectEditor

    @Attribute selfDisplay : SingletonDisplay end
    @Attribute ofDisplay : SingletonDisplay end
    @Attribute ownerDisplay : SingletonDisplay end
    @Attribute documentationDisplay : CodeboxDisplay end
    @Attribute sourceDisplay : CodeboxDisplay end

    @Operation new()
      super();
      self.addTextBoxRules()
    end

    @Operation addContents()
      if not self.layoutManager = null then
        layoutManager.add(self.selfDisplays());
        layoutManager.addSeparator();
        layoutManager.add(Seq{self.sourceDisplay(),self.documentationDisplay()});
        layoutManager.layoutContents()
      else self.error("Layout manager must be set for " + self.toString())
      end
    end

    @Operation selfDisplays()
      self.selfDisplay := self.newSingletonDisplay(CompiledOperationSelfTextField,self.element,"self");
      selfDisplay.field
        .setContex(element)
        .setSlotName("self");
      self.ofDisplay := self.newSingletonDisplay(self.element.of(),"of");
      ofDisplay.field
        .setIsEditable(false)
        .setContex(element)
        .setSlotName("of");
      self.ownerDisplay := self.newSingletonDisplay(self.element.owner(),"owner");
      ownerDisplay.field
        .setContex(element)
        .setSlotName("owner");
      Seq{selfDisplay,ofDisplay,ownerDisplay}
    end

    @Operation documentationDisplay()
      let
        doc = self.element.doc()
      then
        text = if doc.isKindOf(Null) then "" else
          doc.docString()
        end
      then
        textbox = DocumentationTextBox(0,0,0,0,text,true)
          .setContex(element)
          .setSlotName("documentation")
      in
        self.documentationDisplay := self.newCodeboxDisplay(textbox,"documentation");
        documentationDisplay
      end
    end
    
    @Operation refresh()
      super();
      self.addTextBoxRules()
    end

    @Operation sourceDisplay()
      let
        source = self.element.source()
      then
        code =
          if source.isKindOf(Null)
          then ""
          else source
          end
      then
        textbox = CompiledOperationSourceTextBox(0,0,0,0,code,true)
          .setContex(element)
          .setSlotName("source")
      in
        self.sourceDisplay := self.newCodeboxDisplay(textbox,"source");
        sourceDisplay
      end
    end

    @Operation setTextOut(slotName:String,value)
      let valueLabel = value.labelString() in
        if slotName = "documentation" then
          let textbox = documentationDisplay.textbox
          in
            textbox.setText(valueLabel);
            self.commandInterpreter().setText(textbox,valueLabel)
          end
        elseif slotName = "source" then
          let textbox = sourceDisplay.textbox
          in
            textbox.setText(valueLabel);
            self.commandInterpreter().setText(textbox,valueLabel)
          end
        else
          let displays = Seq{selfDisplay,ofDisplay,ownerDisplay} in
            @Find(display,displays)
              when display.label.text.toString() = slotName
              do
                let textComponent = display.field in
                  textComponent.element := value;
                  textComponent.setText(valueLabel);
                  self.commandInterpreter().setText(textComponent,valueLabel)
                end
            end
          end
        end;
        self
      end
    end

    @Operation addTextBoxRules()
      let rules = OCL::OCL.grammar.terminals()->asSeq in
        @For rule inSeq rules do
          sourceDisplay.textbox.addWordRule(rule,FormTextBox::blue)
        end
      end;
      self
    end

    @Operation resetOperation(op:CompiledOperation)
      self.setElement(op);
      self.initForm();
      self.commandInterpreter().clearFormCommand(self);
      self.setName("Properties - " + element.labelString());
      self.refresh()
    end

  end