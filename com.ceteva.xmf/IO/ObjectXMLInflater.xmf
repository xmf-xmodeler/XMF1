parserImport XML::PrintXML;
parserImport XOCL;

import IO;
import XML::Parser;

context IO
  @Class ObjectXMLInflater
  
    @Doc
      An object XML inflater is used to control how objects are
      are mapped from XML to XMF. Typically a formatter has been
      used to write out certain types of object in a bespoke XML
      format. The generic XMLInputChannel is parameterized with
      respect to an inflater so which performs the bespoke
      formatting in reverse. 
    end
    
    @Attribute pred : Operation = @Operation(x) true end (?,!) end
    
    @Constructor(pred) end
    
    @Operation endObject(tag:Buffer,inch:XMLInputChannel)
      inch.endObject()
    end
    
    @Operation result(value,idTable)
      // Perform any processing on the result of inflating the
      // XML source file.
      let walker = ResolveRefs(idTable,pred)
      in walker.walk(value,null)
      end
    end
  
    @Operation startObject(tag:Buffer,atts:Buffer,inch:XMLInputChannel)
      inch.startObject(atts)
    end
    
  end