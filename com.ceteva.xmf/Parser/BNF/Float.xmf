parserImport XOCL;

import Parser::BNF;
import OCL;

context Parser::BNF

  @Class Float extends Recognizer 

	// Succeeds when the next sequence of input characters 
	// constitutes an integer. Leaves the integer on the stack.
	
	@Operation code(outputChannel)
      format(outputChannel," Float ")
    end
    
    @Operation FV():Set(String)
      Set{}
    end
    
    @Operation nextSet(grammar)
      Set{self}
    end

	@Operation parse(machine)
	  if machine.eof()
	  then 
	    if machine.grammar.debug then machine.debug("EOF encountered when expecting a float.") end;
	    machine.fail()
	  else 
	    let token = machine.next()
	    in if token->head = 4
	       then 
	         if machine.grammar.debug then machine.debug("Float succeeds: " + token->at(1)) end;
	         machine.pushStack(token->at(1));
	         machine.consumedChars := token->at(2)
	       else 
	         if machine.grammar.debug then machine.debug("expecting a float, got " + token) end;
	         machine.fail()
	       end
	    end
	  end
	end
	
	@Operation toExp(FV:Set(String))
	  [| Root::Parser::BNF::Float() |]
	end
	
	@Operation transform(FV:Set(String),grammar):Recognizer
      self
    end
  end